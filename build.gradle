buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.2.RELEASE'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'com.github.kt3k.coveralls'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

sourceSets {
    componentTest {
        java.srcDir file('src/component-test/java')
        resources.srcDir file('src/component-test/resources')
    }
}

task componentTest(type: Test) {
    testClassesDir = sourceSets.componentTest.output.classesDir
    classpath = sourceSets.componentTest.runtimeClasspath
}

componentTest {
    jacoco {
        append = true
        destinationFile = file("$buildDir/jacoco/test.exec")
    }
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
 }

 checkstyle {
	toolVersion = "6.0"
	configFile = new File(rootDir, "checkstyle.xml")
}

findbugs {
    sourceSets = [sourceSets.main, sourceSets.test]
}

componentTest.dependsOn test
check.dependsOn componentTest
jar.dependsOn check

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "com.google.guava:guava:18.0"
    compile "org.flywaydb:flyway-core"
    compile "org.apache.commons:commons-csv:1.1"

    testCompile "junit:junit:4.12"
    testCompile "org.assertj:assertj-core:3.1.0"
    testCompile "org.mockito:mockito-core:1.10.19"
    testCompile "com.jayway.restassured:spring-mock-mvc:2.5.0"
    testCompile "org.skyscreamer:jsonassert:1.2.3"
    testCompile "org.springframework.boot:spring-boot-starter-test"
    testRuntime "com.h2database:h2"


    componentTestCompile sourceSets.main.output
    componentTestCompile configurations.compile
    componentTestCompile "org.assertj:assertj-core:3.1.0"
    componentTestCompile "org.springframework.boot:spring-boot-starter-test"
    componentTestCompile "com.jayway.restassured:rest-assured:2.5.0"
    componentTestRuntime "com.h2database:h2"
}

run {
    classpath = sourceSets.componentTest.runtimeClasspath
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}
